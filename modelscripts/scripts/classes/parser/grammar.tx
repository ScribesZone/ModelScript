ClassModel:
    megamodelPart = MegamodelPart
    declarations *= Declaration
;

Declaration:
      EntitydDeclaration
    | SpeechDeclaration
;


//====================================================================
//                        Symbolic syntax
//====================================================================

EntitydDeclaration:
      DataType
    | Enumeration
    | Class
    | Association
    | Invariant
;

DataType:
    'datatype' name=ID (o_
        (textBlock = TextBlock) ?
    _o) ?
    X
;

Enumeration:
    'enumeration' name=ID o_
        (textBlock = TextBlock) ?
        literals += EnumerationLiteral
    _o
    X
;

EnumerationLiteral:
    name=ID
        (o_
            textBlock=TextBlock
        _o)?
    X
;


//----------------------------------------------------------

Class:  // common
    (isAbstract ?= 'abstract')
    'class' name=ID
    ('extends' superclasses *= ID [',']) ?
    (o_
        (textBlock = TextBlock)?
        attributeCompartment=AttributesCompartment ?
        operationCompartment=OperationsCompartment ?
        invariantCompartment=InvariantsCompartment ?
        permissionCompartment=PermissionsCompartment ?
    _o)?
    X
;

AttributesCompartment:  // common
    'attributes'
    (o_
        attributes *= Attribute
    _o)?
    X
;

Attribute:
    (isDerived ?= '/') ?
    (visibility = Visibility) ?
    name=ID ':' type=ID
    (cardinality=Cardinality) ?
    ( '{' tags += ID '}' )?
    (o_
        textBlock=TextBlock
    _o)?
    X
;

OperationsCompartment:
    'operations'
    (o_
        operations *= Operation
    _o)?
    X
;

Operation:
    name=ID
    '('
        arguments*=Argument[',']
    ')'
    (':' returnType=ID)?
    (o_
        textBlock=TextBlock
    _o)?
    X

;

Arguments:
    arguments *= Argument[',']
;


Argument:
    name=ID ':' type=ID
;

InvariantsCompartment:
    'invariants'
    X
;

PermissionsCompartment:
    'permissions'
    X
;


//----------------------------------------------------------


Association:
    kind=AssociationKind o_
        (textBlock=TextBlock) ?
        roleCompartment=RoleCompartment
        attributeCompartment=AttributesCompartment ?
        operationCompartment=OperationsCompartment ?
    _o
    X
;

AssociationKind:
      'association'
    | 'composition'
    | 'aggregation'
;

RoleCompartment:
    'roles' o_
        from=Role
        to=Role
    _o
    X
;

Role:
    name=ID ':' class=[Class] cardinality=Cardinality ?
    (o_
        textBlock=TextBlock
    _o)?
    X
;

//----------------------------------------------------------

Invariant:
    'invariant' X
;

Cardinality:
//    '[' max='*' ']'
//    | '[' min=CardinalityMin ']'
//    | '[' min=CardinalityMin ? ('..' max=CardinalityMax)? ']'
   '[' min=Multiplicity ? ('..' max=Multiplicity) ? ']'
;

Multiplicity:
    INT | '*'
;

Visibility:
      '-'
    | '+'
    | '%'
    | '~'
;


//====================================================================
//                        Speech syntax
//====================================================================

SpeechDeclaration:
      SpeechClassDefinition
    | SpeechInheritance
    | SpeechAttributeDefinition
    | SpeechAssocForwardDefinition
    | SpeechAssocConstraint
;


SpeechClassDefinition:
      FRClassDefinition
    | ENClassDefinition
;

FRClassDefinition:
    'Le' 'concept' 'de' class=ID  'est' 'important' '.'
    X
;

ENClassDefinition:
    'The' 'concept' 'of' class=ID 'is' 'important' '.'
    X
;

SpeechInheritance:
      FRClassInheritance
    | ENClassInheritance
;

FRClassInheritance:
    Un_ class=ID 'est' un_ superclass=ID '.'
    X
;

ENClassInheritance:
    A_ class=ID 'is' a_ superclass=ID '.'
    X
;

SpeechAttributeDefinition:
      FRAttributeDefinition
    | ENAttributeDefinition
;

FRAttributeDefinition:
    Le_ attribute=ID d_un_ class=ID 'est' un_ type=ID '.' X
;

ENAttributeDefinition:
    'The' attribute=ID 'of' a_ class=ID 'is' a_ type=ID '.' X
;

SpeechAssocForwardDefinition:
      ENAssocForwardDefinition
    | FRAssocForwardDefinition
;

ENAssocForwardDefinition:
    A_ class1=ID
    association=ID
    'of'? cardinality=SpeechAssocCardinality
    role2=ID
    'of'? 'type' class2=ID
    '.'
    X
;

FRAssocForwardDefinition:
    Un_ class1=ID
    association=ID
    de_? cardinality=SpeechAssocCardinality
    role2=ID
    de_? 'type' class2=ID
    '.'
    X
;


SpeechAssocReverseDefinition:
      ENAssocReverseDefinition
    | FRAssocReverseDefinition
;

ENAssocReverseDefinition:
    A_ class2=ID
    'has' cardinality=SpeechAssocCardinality
    role1=ID
    'of' 'type' class1=ID
    '.'
    X
;

FRAssocReverseDefinition:
    Un_ class2=ID
    'a' cardinality=SpeechAssocCardinality
    role1=ID
    'de' 'type' class1=ID
    '.'
    X
;

SpeechAssocCardinality:
      SpeechAssocCardinality_0N
    | SpeechAssocCardinality_1N
    | SpeechAssocCardinality_01
    | SpeechAssocCardinality_1
    | SpeechAssocCardinality_XY
;

SpeechAssocCardinality_0N:
      ('zero' 'or' 'many')
    | ('zero' 'ou' 'plusieurs')
;

SpeechAssocCardinality_1N:
      ('one' 'or' 'many')
    | (('un'|'une') 'ou' 'plusieurs')
;

SpeechAssocCardinality_01:
      ('zero' 'or' 'one')
    | ('zero' 'ou' un_)
;

SpeechAssocCardinality_1:
      'one'
    | un_
;

SpeechAssocCardinality_XY:
      ('between' min=INT 'and' max=INT)
    | ('entre' min=INT 'et' max=INT)
;


SpeechAssocConstraint:
      ENAssocConstraint
    | FRAssocConstraint
;

ENAssocConstraint:
    'If' var2a=ID
    'is' card2=ENAssocConstraintCardinality
    role2=ID
    'of' 'a' class1=ID
    var1a=ID
    o_
        'then' var1b=ID
        'is' card1=ENAssocConstraintCardinality
        role1=ID
        'of' ( 'the' class2=ID )
        var2b=ID
        '.'
        X
    _o
    X
;

ENAssocConstraintCardinality:
      'the'
    | 'one' 'of' 'the'
;

FRAssocConstraint:
    'Si' var2a=ID
    'est' card2=FRAssocConstraintCardinality
    role2=ID
    d_un_ class1=ID
    var1a=ID
    o_
        'alors' var1b=ID
        'est' card1=FRAssocConstraintCardinality
        role1=ID
        (     (du_ class2=ID var2b=ID)
            | ('de' var2b=ID)
            )
        '.'
        X
    _o
    X
;


FRAssocConstraintCardinality:
      le_
    | "l'" un_ 'des'
;

Le_:
    'Le' | 'La' | "L'"
;

le_:
    'le' | 'la'
;

d_un_:
    'd' "'" ('une'|'un')
;

de_:
    "d'" | 'de'
;

du_:
    'du' | 'de' 'la'
;

Un_ :
    'Une' | 'Un'
;

un_ :
    'une' | 'un'
;

A_ :
    'A' | 'An'
;

a_ :
    'a' | 'an'
;

